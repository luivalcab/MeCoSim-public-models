@model<infEnv_symport_antiport>

def main()
{
		
	call module_init_conf(n,m);
	call module_rules(n,m);
	call module_input_instance();

}

def module_init_conf(n,m)
{
	
	@mu = [[[]'2 []'3]'1]'environment;
	
	@msInfEnv += S;
	@msInfEnv += d{i,j,k},_d{i,j,k} : 1 <= i <= n, 1 <= j <= m, 1 <= k <= n - 1;
	@msInfEnv += T{i},F{i} : 1 <= i <= n;
	@msInfEnv += f{r} : 1 <= r <= n + 2*m;
	@msInfEnv += delta{j,r} : 0 <= j <= m, 1 <= r <= n - 1;
	@msInfEnv += E{j} : 0 <= j <= m;
	@msInfEnv += e{i,j},_e{i,j} : 1 <= i <= n, 1 <= j <= m;
	
	@ms(1) += f{0},yes;
	@ms(1) += fp{r} : 1 <= r <= n + 2*m + 1;
	@ms(1) += delta{j,0} : 0 <= j <= m;
	
	@ms(2) += a{i} : 1 <= i <= n;
	@ms(2) += b,c;
	
	@ms(3) += fp{0},no;
	
}

def module_rules(n,m)
{
	
	/* R1 */
	
	d{i,j,1}*2[x{i,j}]'1 --> x{i,j}[d{i,j,1}*2]'1 : 1 <= i <= n, 1 <= j <= m;
	_d{i,j,1}*2[_x{i,j}]'1 --> _x{i,j}[_d{i,j,1}*2]'1 : 1 <= i <= n, 1 <= j <= m;
	
	d{i,j,k+1}*2[d{i,j,k}]'1 --> d{i,j,k}[d{i,j,k+1}*2]'1 : 1 <= i <= n, 1 <= j <= m, 1 <= k <= n - 2;
	_d{i,j,k+1}*2[_d{i,j,k}]'1 --> _d{i,j,k}[_d{i,j,k+1}*2]'1 : 1 <= i <= n, 1 <= j <= m, 1 <= k <= n - 2;
	
	e{i,j}[d{i,j,n-1}]'1 --> d{i,j,n-1}[e{i,j}]'1  : 1 <= i <= n, 1 <= j <= m;
	_e{i,j}[_d{i,j,n-1}]'1 --> _d{i,j,n-1}[_e{i,j}]'1  : 1 <= i <= n, 1 <= j <= m;
	
	[E{0},f{n+2*m},yes]'1 --> E{0},f{n+2*m},yes[]'1;
	
	[f{n+2*m},no]'1 --> f{n+2*m},no[]'1;
	
	delta{j,r+1}*2[delta{j,r}]'1 --> delta{j,r}[delta{j,r+1}*2]'1 : 0 <= r <= n - 2, 0 <= j <= m;
	E{j}*2[delta{j,n-1}]'1 --> delta{j,n-1}[E{j}*2]'1 : 0 <= j <= m;
	f{i+1}[f{i}]'1 --> f{i}[f{i+1}]'1 : 0 <= i <= n + 2*m - 1;
	
	/* R2 */
	
	[a{i}]'2 --> [T{i}]'2 [F{i}]'2 : 1 <= i <= n;
	
	E{1}[b]'2 --> b[E{1}]'2;
	E{0}[c]'2 --> c[E{0}]'2;
	
	e{i,j}[E{j},T{i}]'2 --> E{j},T{i}[e{i,j}]'2 : 1 <= i <= n, 1 <= j <= m;
	_e{i,j}[E{j},F{i}]'2 --> E{j},F{i}[_e{i,j}]'2 : 1 <= i <= n, 1 <= j <= m;
	
	E{j+1},T{i}[e{i,j}]'2 --> e{i,j}[E{j+1},T{i}]'2 : 1 <= i <= n, 1 <= j <= m - 1;
	E{j+1},F{i}[_e{i,j}]'2 --> _e{i,j}[E{j+1},F{i}]'2 : 1 <= i <= n, 1 <= j <= m - 1;
	
	[e{i,m},E{0}]'2 --> e{i,m},E{0}[]'2 : 1 <= i <= n;
	[_e{i,m},E{0}]'2 --> _e{i,m},E{0}[]'2 : 1 <= i <= n;
	
	/* R3 */
	
	fp{r+1}[fp{r}]'3 --> fp{r}[fp{r+1}]'3 : 0 <= r <= n + 2*m;
	[fp{n+2*m+1},no]'3 --> fp{n+2*m+1},no[]'3;
	
}

def module_input_instance()
{
/* We define here the input for the P system */
/* Let suppose m = clause number and n = variable numbers */
	
	@ms(1) += _x{variable{i},clause{i}}*valn{i}, x{variable{i},clause{i}}*val{i} : 1<=i<=nvals;
	
}
	
